// Pachelbel’s Canon
key_signature = D_major

// bass line
bass = half:@-1[ 1@1  5 6 3 4 1 4 5 ]

// opening line
intro = half:(@1[ 3 2 1 ] . [ 7 6 5 6 7 ])
next_intro = FinishWith half:2 (Relative -3 intro)


// verse 1
climb = quarter:[ 1 3 ]
fall = Reverse climb
slip = quarter:[ 5 4 ]
// it’s all thirds!!!
verse_1 = climb . slip . fall . Relative -3 (slip . fall) . (q:[ 1 5 ]) . Relative 4 climb . slip
next_verse_1 = fall . (q:[ 2 7 ]) . Octave 1 climb . (q:[ 5@1  5 ]) . \
        Relative 4 fall . Relative 3 fall . (q:[ 1  1@1 ]) . ([ q+e e ] : [ 1@1  7 ])

// verse 2
verse_2 = eighth:([ 1@1  7  1@1 1 7@-1 5 2 3 1  1@1  7 6 5 6 ] . @1[ 3 5 6 ])
// it’s all scales!!!
climb = eighth : Scale 1 4
fall = Reverse climb
next_verse_2 = EndWith e:4@1 (Relative 4 fall) . Relative 7 fall . fall . \
StartWith e:2 (Relative 2 fall) . climb . e:[ 5 1 5 4 ] . \
        StartWith e:3 (Relative 4 fall) . Relative 2 fall . e:[ 1  6@-1  6 7 ] . \
        Relative 5 fall . EndWith e:6 fall . StartWith e:5 (Relative 4 fall)

// bridge
qqh = Repeat 2 [ q q h ]
bridge = qqh:[ 3@-1  3 2 ~ 1 3 ] . h:@-1[ 6 5 6 7 ] . qqh:[ 1@1  1  7@-1  ~  6@-1  1 ] . \
        (h+q):1 . q:[ 1 4 2 5 ]
bridge = Octave 1 bridge

// chorus
rhythm = Repeat 2 [ e/2 e/4 e/4 ] . Repeat 8 [ e/4 ]
tones = @1[ 5 3 4 5 3 4 5 ] . Scale 5 4@1 . \
@1[ 3 1 2 3 ] . [ 3 4 5 6 5 4 5 3 4 5 ] . \
[ 4 6 5 4 3 2 3 2 ] . Scale 1 6 . \
[ 4 6 5 6 7 1@1 ] . Scale 5 5@1 . \
@1[ 3 1 2 3 2 1 2  7@-1  1 2 3 2 1  7@-1 ] . \
[ 1@1  6 7  1@1  1 2 3 4 3 2 3  1@1  7  1@1 ] . \
[ 6  1@1  7 6 5 4 5 4 ] . Scale 3 1@1 . \
[ 6  1@1  7  1@1  7 6 7 ] . @1[ 1 2 1  7@-1  1 ] . [ 6 7 ]
chorus = Repeat 8 rhythm : tones

// entire song
melody = intro . next_intro . verse_1 . next_verse_1 . \
verse_2 . next_verse_2 . bridge . chorus
first_part = melody . whole:1@1
second_part = (2*whole):~ . RemoveEnd (2*whole) melody . whole:3@1
bass_part = Extend (Length melody) bass_part . whole:5

canon =  Parallel first_part second_part bass_part
Render “canon.wav” canon
