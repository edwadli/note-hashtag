// Pachelbel’s Canon
key_sig = d_major

// bass line
bass = half:@(-1)[ 1@1  5 6 3 4 1 4 5 ]

// opening line
intro = half:(@1[ 3 2 1 ] . [ 7 6 5 6 7 ])
next_intro = EndWith (half:2) (Relative (-2) intro)


// verse 1
climb = quarter:[ 1 3 ]
fall = Reverse climb
slip = quarter:[ 5 4 ]
// it’s all thirds!!!
verse_1 = climb . slip . fall . Relative (-2) (slip . fall) . (q:[ 1 5 ]) . Relative 3 climb . slip
next_verse_1 = fall . (q:[ 2 7 ]) . Octave 1 climb . (q:[ 5@1  5 ]) . \
        Relative 3 fall . Relative 2 fall . (q:[ 1  1@1 ]) . ([ (q+e) e ] : [ 1@1  7 ])

// verse 2
verse_2 = eighth:([ 1@1  7  1@1 1 7@(-1) 5 2 3 1  1@1  7 6 5 6 ] . @1[ 5 6 ])
// it’s all scales!!!
climb = eighth : (Scale (1@0) (4@0))
fall = Reverse climb
next_verse_2 = EndWith (e:4@1) (Relative 3 fall) . Relative 6 fall . fall . \
        StartWith (e:2) (Relative 1 fall) . climb . e:[ 5 1 5 4 ] . \
        StartWith (e:3) (Relative 3 fall) . Relative 1 fall . e:[ 1  6@(-1)  6 7 ] . \
        Relative 4 fall . EndWith (e:6) fall . StartWith (e:5) (Relative 3 fall)

// bridge
qqh = Repeat 2 [ q q h ]
bridge = qqh:[ 3@(-1)  3 2 ~ 1 3 ] . h:@(-1)[ 6 5 6 7 ] . qqh:[ 1@1  1  7@(-1)  ~  6@(-1)  1 ] . \
        (h+q):1 . q:[ 1 1 4 2 5 ]
bridge = Octave 1 bridge

// chorus
rhythm = Repeat 2 [ e (e/2.0) (e/2.0) ] . Repeat 8 [ (e/2.0) ]
tones = @1[ 5 3 4 5 3 4 5 ] . Scale (5@0) (4@1) . \
@1[ 3 1 2 3 ] . [ 3 4 5 6 5 4 5 3 4 5 ] . \
[ 4 6 5 4 3 2 3 2 ] . Scale (1@0) (6@0) . \
[ 4 6 5 6 7 1@1 ] . Scale (5@0) (5@1) . \
@1[ 3 1 2 3 2 1 2  7@(-1)  1 2 3 2 1  7@(-1) ] . \
[ 1@1  6 7  1@1  1 2 3 4 3 2 3  1@1  7  1@1 ] . \
[ 6  1@1  7 6 5 4 5 4 ] . Scale (3@0) (1@1) . \
[ 6  1@1  7  1@1  7 6 7 ] . @1[ 1 2 1  7@(-1)  1 ] . [ 6 7 ]
chorus = Repeat 8 rhythm : tones

// entire song
melody = intro . next_intro . verse_1 . next_verse_1 . \
  verse_2 . next_verse_2 . bridge . chorus
first_part = melody . whole:1@1
second_part = (4.0*whole):~ . RemoveEnd (4.0*whole) melody . whole:3@1
bass_part = Extend (Length melody) bass . whole:5

canon =  Parallel { first_part second_part bass_part }
canon$volumes = { 1.0 1.0 0.5 }
Render canon "canon.wav"

